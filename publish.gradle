nexusPublishing {
    useStaging = true
    repositories {
        sonatype {
            // because we registered in Sonatype after 24 Feb 2021, we provide these URIs
            // see: https://github.com/gradle-nexus/publish-plugin/blob/master/README.md
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

configure(subprojects.findAll {it.name != 'buildSrc'}) {
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'signing'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    signing {
        useInMemoryPgpKeys(
                System.getenv("MAVEN_CENTRAL_SIGNING_KEY"),
                System.getenv("MAVEN_CENTRAL_SIGNING_KEY_PASSWORD")
        )
        sign publishing.publications
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = 'uk.gov.ida'
                pom {
                    name = project.name
                    packaging = 'jar'
                    description = project.description
                    url = 'https://github.com/alphagov/verify-saml-libs'
                    artifactId = project.name

                    scm {
                        url = 'https://github.com/alphagov/verify-saml-libs'
                        connection = 'scm:git:git://github.com/alphagov/verify-saml-libs.git'
                        developerConnection = 'scm:git:ssh://git@github.com:alphagov/verify-saml-libs.git'
                    }

                    licenses {
                        license {
                            name = 'MIT Licence'
                            url = 'https://github.com/alphagov/verify-saml-libs/blob/master/LICENCE'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            name = 'GDS Developers'
                        }
                    }
                } // pom
            }
        }
    }
}
