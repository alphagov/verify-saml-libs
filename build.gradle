plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

ext {
    opensaml_version = "3.4.6"
    dropwizard_version = "2.1.5"
    ida_utils_version = "414"
    build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
}

apply from: 'publish.gradle'

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'java'

    group = "uk.gov.ida"
    version = "$build_version"
}

repositories {
    mavenCentral()
}

subprojects {
    configurations.all {
        // These are set to provide compatibility with the MSA and the VSP which need to stay on Java8 for services.
        // Please don't bump unless we've agreed to release the MSA and VSP with a later version.
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
            maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
            mavenCentral()
        }
        else {
            maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
        }
    }

    task allDeps(type: DependencyReportTask) {}

    configurations {
        slf4j
        jaxb
        guice
        opensaml
        dropwizard
        xml_utils
        test_deps
        security
    }

    dependencies {
        slf4j 'org.slf4j:slf4j-api:1.7.26'

        jaxb 'javax.xml.bind:jaxb-api:2.3.1'

        guice 'com.google.guava:guava:31.1-jre',
                'com.google.inject:guice:5.1.0'

        dropwizard "io.dropwizard:dropwizard-core:$dropwizard_version",
                "io.dropwizard:dropwizard-client:$dropwizard_version",
                "io.dropwizard:dropwizard-jackson:$dropwizard_version"

        opensaml "org.opensaml:opensaml-core:$opensaml_version",
                "org.opensaml:opensaml-saml-impl:$opensaml_version",
                "org.opensaml:opensaml-xmlsec-api:$opensaml_version",
                "org.opensaml:opensaml-security-api:$opensaml_version",
                "org.opensaml:opensaml-saml-api:$opensaml_version",
                "org.opensaml:opensaml-xmlsec-impl:$opensaml_version",
                'net.shibboleth.utilities:java-support:7.5.0'

        security 'com.nimbusds:nimbus-jose-jwt:9.24.3',
                "uk.gov.ida:security-utils:2.0.0-$ida_utils_version"

        test_deps   'uk.gov.ida:common-test-utils:2.0.0-69',
                    'com.github.tomakehurst:wiremock-standalone:2.27.2',
                    "io.dropwizard:dropwizard-testing:$dropwizard_version",
                    'org.hamcrest:hamcrest-library:2.2',
                    'org.assertj:assertj-joda-time:2.2.0',
                    'org.assertj:assertj-core:3.23.1',
                    'org.junit.jupiter:junit-jupiter-api:5.9.0',
                    'uk.gov.ida:verify-dev-pki:2.0.0-48',
                    'org.mockito:mockito-core:4.7.0',
                    "org.mockito:mockito-junit-jupiter:4.7.0"

        xml_utils "uk.gov.ida:common-utils:2.0.0-$ida_utils_version"
    }
}

